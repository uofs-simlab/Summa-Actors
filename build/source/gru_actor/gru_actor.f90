module gru_actor
USE,intrinsic :: iso_c_binding
USE nr_type
USE globalData,only:integerMissing
USE globalData,only:realMissing

implicit none
public::f_getNumHruInGru
public::f_initGru
public::f_setGruTolerances
public::setupGRU_fortran
public::readGRURestart_fortran
public::setTimeZoneOffsetGRU_fortran
public::readGRUForcing_fortran
public::runGRU_fortran
public::writeGRUOutput_fortran
public::f_resetGruStrucNSnow
private::setupGRU
private::allocateOutputBuffer
private::alloc_outputStruc
private::allocateDat_rkind
private::allocateDat_int
private::is_var_desired

contains

subroutine f_getNumHruInGru(indx_gru, num_hru) bind(C, name="f_getNumHruInGru")
  USE globalData,only:gru_struc
  implicit none
  integer(c_int), intent(in)  :: indx_gru
  integer(c_int), intent(out) :: num_hru

  num_hru = gru_struc(indx_gru)%hruCount
end subroutine f_getNumHruInGru

subroutine f_setGruTolerances(handle_gru_data, be_steps, &
  ! Relative Tolerances
  rel_tol_temp_cas, rel_tol_temp_veg, rel_tol_wat_veg, &
  rel_tol_temp_soil_snow, rel_tol_wat_snow, rel_tol_matric, rel_tol_aquifr, &
  ! Absolute Tolerances 
  abs_tol_temp_cas, abs_tol_temp_veg, &
  abs_tol_wat_veg, abs_tol_temp_snow_soil, abs_tol_wat_snow, abs_tol_matric, &
  abs_tol_aquifr)  bind(C, name="f_setGruTolerances")

  USE global_tol
  USE actor_data_types,only:gru_type
  USE var_lookup,only: iLookPARAM

  implicit none
  type(c_ptr), intent(in),value   :: handle_gru_data
  integer(c_int), intent(in)      :: be_steps
  ! Relative Tolerances
  ! real(c_double), intent(in)       :: rel_tol
  real(c_double), intent(inout)    :: rel_tol_temp_cas
  real(c_double), intent(inout)    :: rel_tol_temp_veg
  real(c_double), intent(inout)    :: rel_tol_wat_veg
  real(c_double), intent(inout)    :: rel_tol_temp_soil_snow
  real(c_double), intent(inout)    :: rel_tol_wat_snow
  real(c_double), intent(inout)    :: rel_tol_matric
  real(c_double), intent(inout)    :: rel_tol_aquifr
  ! Absolute Tolerances
  ! real(c_double), intent(in)       :: abs_tol
  ! real(c_double), intent(in)       :: abs_tolWat
  ! real(c_double), intent(in)       :: abs_tolNrg
  real(c_double), intent(inout)    :: abs_tol_temp_cas
  real(c_double), intent(inout)    :: abs_tol_temp_veg
  real(c_double), intent(inout)    :: abs_tol_wat_veg
  real(c_double), intent(inout)    :: abs_tol_temp_snow_soil
  real(c_double), intent(inout)    :: abs_tol_wat_snow
  real(c_double), intent(inout)    :: abs_tol_matric
  real(c_double), intent(inout)    :: abs_tol_aquifr

  ! Local Varaibles
  integer(i4b)                  :: iHRU
  integer(i4b)                  :: iDOM

  type(gru_type),pointer :: gru_data
  call c_f_pointer(handle_gru_data, gru_data)

  do iHRU = 1, size(gru_data%hru)
   do iDOM = 1, size(gru_data%hru(iHRU)%mparStruct%dom)
    if (be_steps>0) then
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%be_steps)%dat(1) = be_steps
    end if

    ! Only override the solver tolerances when real values were supplied (the IDA path, or
    ! adaptive-tolerance tightening after a failure).  f_getInitTolerance returns the -9999
    ! sentinel for the homegrown / kinsol path, meaning "keep the param-file / SUMMA default
    ! tolerances" -- writing -9999 into the params corrupts the backward-Euler convergence test
    ! and makes every timestep converge to a different answer than non-actors SUMMA.
    if (rel_tol_matric > 0._c_double) then
      ! Set rtols
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolTempCas)%dat(1) = rel_tol_temp_cas
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolTempVeg)%dat(1) = rel_tol_temp_veg
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolWatVeg)%dat(1) = rel_tol_wat_veg
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolTempSoilSnow)%dat(1) = rel_tol_temp_soil_snow
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolWatSnow)%dat(1) = rel_tol_wat_snow
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolMatric)%dat(1) = rel_tol_matric
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%relTolAquifr)%dat(1) = rel_tol_aquifr

      ! Set atols
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolTempCas)%dat(1) = abs_tol_temp_cas
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolTempVeg)%dat(1) = abs_tol_temp_veg
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolWatVeg)%dat(1) = abs_tol_wat_veg
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolTempSoilSnow)%dat(1) = abs_tol_temp_snow_soil
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolWatSnow)%dat(1) = abs_tol_wat_snow
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolMatric)%dat(1) = abs_tol_matric
      gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%absTolAquifr)%dat(1) = abs_tol_aquifr
    end if

   end do
  end do

end subroutine f_setGruTolerances

subroutine setupGRU(iGRU, err, message)
  USE summa_init_struc,only:init_struc
  USE globalData,only:gru_struc
  USE globalData,only:model_decisions                         ! model decision structure
  USE globalData,only:greenVegFrac_monthly                    ! fraction of green vegetation in each month (0-1)
  
  USE var_lookup,only:iLookTYPE
  USE var_lookup,only:iLookID
  USE var_lookup,only:iLookDECISIONS
  USE var_lookup,only:iLookPARAM
  USE var_lookup,only:iLookATTR
  USE var_lookup,only:iLookBVAR
  
  USE NOAHMP_VEG_PARAMETERS,only:HVT,HVB                      ! height at the top and bottom of vegetation (vegType)
  USE NOAHMP_VEG_PARAMETERS,only:SAIM,LAIM                    ! 2-d tables for stem area index and leaf area index (vegType,month)
  
  USE paramCheck_module,only:paramCheck                       ! module to check consistency of model parameters
  ! look-up values for the choice of variable in energy equations (BE residual or IDA state variable)
  USE mDecisions_module,only:&
    closedForm,              &            ! use temperature with closed form heat capacity
    enthalpyForm,            &            ! use enthalpy with soil temperature-enthalpy lookup tables
    enthalpyFormAN                        ! use enthalpy with soil temperature-enthalpy analytical solution
  USE convertEnthalpyTemp_module,only:T2H_lookup_snWat               ! module to calculate a look-up table for the snow temperature-enthalpy conversion
  USE convertEnthalpyTemp_module,only:T2L_lookup_soil                ! module to calculate a look-up table for the soil temperature-enthalpy conversion

  USE var_derive_module,only:fracFuture                       ! module to calculate the fraction of runoff in future time steps (time delay histogram)

  USE globalData,only:realMissing                             ! missing real value
  USE globalData,only:nMeltingIceLayers                       ! number of glacier ice layers that can melt
  USE globalData,only:nLakeIceLayers_poss                     ! number of possible lake ice layers
  USE globalData,only:upland                                  ! horizontal domain type for upland areas

  ! named variables to define LAI decisions
  USE mDecisions_module,only:&
      monthlyTable,& ! LAI/SAI taken directly from a monthly table for different vegetation classes
      specified      ! LAI/SAI computed from green vegetation fraction and winterSAI and summerLAI parameters
  implicit none
  ! Dum
  integer(c_int), intent(in)      :: iGRU
  integer(c_int), intent(out)     :: err
  character(len=256), intent(out) :: message

  ! Local Variables
  character(len=256) :: cmessage
  integer(i4b)       :: iHRU, jHRU, kHRU, iDOM
  logical            :: needLookup_soil
  logical            :: needLookup_ice

  summaVars: associate(&
    lookupStruct         =>init_struc%lookupStruct         , & ! x%gru(:)%hru(:)%z(:)%var(:)%lookup(:) -- lookup tables
    ! statistics structures
    forcStat             => init_struc%forcStat            , & ! x%gru(:)%hru(:)%var(:)%dat -- model forcing data
    progStat             => init_struc%progStat            , & ! x%gru(:)%hru(:)%var(:)%dat -- model prognostic (state) variables
    diagStat             => init_struc%diagStat            , & ! x%gru(:)%hru(:)%var(:)%dat -- model diagnostic variables
    fluxStat             => init_struc%fluxStat            , & ! x%gru(:)%hru(:)%var(:)%dat -- model fluxes
    indxStat             => init_struc%indxStat            , & ! x%gru(:)%hru(:)%var(:)%dat -- model indices
    bvarStat             => init_struc%bvarStat            , & ! x%gru(:)%var(:)%dat        -- basin-average variables

    ! primary data structures (scalars)
    timeStruct           => init_struc%timeStruct          , & ! x%var(:)                   -- model time data
    forcStruct           => init_struc%forcStruct          , & ! x%gru(:)%hru(:)%var(:)     -- model forcing data
    attrStruct           => init_struc%attrStruct          , & ! x%gru(:)%hru(:)%var(:)     -- local attributes for each HRU
    typeStruct           => init_struc%typeStruct          , & ! x%gru(:)%hru(:)%var(:)     -- local classification of soil veg etc. for each HRU
    idStruct             => init_struc%idStruct            , & ! x%gru(:)%hru(:)%var(:)     --

    ! primary data structures (variable length vectors)
    indxStruct           => init_struc%indxStruct          , & ! x%gru(:)%hru(:)%var(:)%dat -- model indices
    mparStruct           => init_struc%mparStruct          , & ! x%gru(:)%hru(:)%var(:)%dat -- model parameters
    progStruct           => init_struc%progStruct          , & ! x%gru(:)%hru(:)%var(:)%dat -- model prognostic (state) variables
    diagStruct           => init_struc%diagStruct          , & ! x%gru(:)%hru(:)%var(:)%dat -- model diagnostic variables
    fluxStruct           => init_struc%fluxStruct          , & ! x%gru(:)%hru(:)%var(:)%dat -- model fluxes

    ! basin-average structures
    bparStruct           => init_struc%bparStruct          , & ! x%gru(:)%var(:)            -- basin-average parameters
    bvarStruct           => init_struc%bvarStruct          , & ! x%gru(:)%var(:)%dat        -- basin-average variables

    ! ancillary data structures
    dparStruct           => init_struc%dparStruct          , &  ! x%gru(:)%hru(:)%var(:)     -- default model parameters

     ! run time variables
    computeVegFlux       => init_struc%computeVegFlux      , & ! flag to indicate if we are computing fluxes over vegetation (.false. means veg is buried with snow)
    dt_init              => init_struc%dt_init             , & ! used to initialize the length of the sub-step for each HRU
    upArea               => init_struc%upArea              , & ! area upslope of each HRU
    
    ! miscellaneous variables
    nGRU                 => init_struc%nGRU              , & ! number of grouped response units
    nHRU                 => init_struc%nHRU                & ! number of global hydrologic response units
  )

  ! *****************************************************************************
  ! *** compute derived model variables that are pretty much constant for the basin as a whole
  ! *****************************************************************************
  ! calculate the fraction of runoff in future time steps
  call fracFuture(bparStruct%gru(iGRU),        &  ! data structure of basin-average model parameters
                  bvarStruct%gru(iGRU),        &  ! data structure of basin-average variables
                  err,cmessage)                   ! error control
  if(err/=0)then; message=trim(message)//trim(cmessage); return; endif

  ! initialize glacier runoff / geometry-update variables (overwritten by initial conditions file values if present)
  bvarStruct%gru(iGRU)%var(iLookBVAR%glacIceRunoffFuture)%dat  = 0._rkind
  bvarStruct%gru(iGRU)%var(iLookBVAR%glacSnowRunoffFuture)%dat = 0._rkind
  bvarStruct%gru(iGRU)%var(iLookBVAR%glacFirnRunoffFuture)%dat = 0._rkind
  bvarStruct%gru(iGRU)%var(iLookBVAR%updateJulDay)%dat     = realMissing
  bvarStruct%gru(iGRU)%var(iLookBVAR%updateJulDayNext)%dat = realMissing

  ! loop through local HRUs
  do iHRU=1,gru_struc(iGRU)%hruCount

   kHRU=0
   ! check the network topology (only expect there to be one downslope HRU)
   do jHRU=1,gru_struc(iGRU)%hruCount
    if(typeStruct%gru(iGRU)%hru(iHRU)%var(iLookTYPE%downHRUindex) == idStruct%gru(iGRU)%hru(jHRU)%var(iLookID%hruId))then
     if(kHRU==0)then  ! check there is a unique match
      kHRU=jHRU
     else
      message=trim(message)//'only expect there to be one downslope HRU'; return
     end if  ! (check there is a unique match)
    end if  ! (if identified a downslope HRU)
   end do

   do iDOM=1,gru_struc(iGRU)%hruInfo(iHRU)%domCount

    ! check that the parameters are consistent
    call paramCheck(mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM),err,cmessage)
    if(err/=0)then; message=trim(message)//trim(cmessage); return; endif

    ! calculate a look-up table for the temperature-enthalpy conversion of snow (and glacier/lake ice if needed)
    needLookup_ice = .false.
    if(nMeltingIceLayers - gru_struc(iGRU)%hruInfo(iHRU)%domInfo(iDOM)%nGlce > 1) needLookup_ice = .true.
    if(nLakeIceLayers_poss > 1 .and. gru_struc(iGRU)%hruInfo(iHRU)%domInfo(iDOM)%nLake > 0) needLookup_ice = .true.
    call T2H_lookup_snWat(mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM),needLookup_ice,err,cmessage)
    if(err/=0)then; message=trim(message)//trim(cmessage); return; endif

    ! calculate a lookup table for the temperature-enthalpy conversion of soil
    needLookup_soil = .false.
    if(model_decisions(iLookDECISIONS%nrgConserv)%iDecision == enthalpyForm .and. &
       gru_struc(iGRU)%hruInfo(iHRU)%domInfo(iDOM)%nSoil > 0) needLookup_soil = .true.
    if(needLookup_soil)then
      call T2L_lookup_soil(gru_struc(iGRU)%hruInfo(iHRU)%domInfo(iDOM)%nSoil, &   ! intent(in):    number of soil layers
                           mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM),          &   ! intent(in):    parameter data structure
                           lookupStruct%gru(iGRU)%hru(iHRU)%dom(iDOM),        &   ! intent(inout): lookup table data structure
                           err,cmessage)                                          ! intent(out):   error control
      if(err/=0)then; message=trim(message)//trim(cmessage); return; endif
    endif

    ! vegetation parameters for the upland domain only
    if (gru_struc(iGRU)%hruInfo(iHRU)%domInfo(iDOM)%dom_type==upland)then
      HVT(typeStruct%gru(iGRU)%hru(iHRU)%var(iLookTYPE%vegTypeIndex)) = mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM)%var(iLookPARAM%heightCanopyTop)%dat(1)
      HVB(typeStruct%gru(iGRU)%hru(iHRU)%var(iLookTYPE%vegTypeIndex)) = mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM)%var(iLookPARAM%heightCanopyBottom)%dat(1)
      if(model_decisions(iLookDECISIONS%LAI_method)%iDecision == specified)then
        SAIM(typeStruct%gru(iGRU)%hru(iHRU)%var(iLookTYPE%vegTypeIndex),:) = mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM)%var(iLookPARAM%winterSAI)%dat(1)
        LAIM(typeStruct%gru(iGRU)%hru(iHRU)%var(iLookTYPE%vegTypeIndex),:) = mparStruct%gru(iGRU)%hru(iHRU)%dom(iDOM)%var(iLookPARAM%summerLAI)%dat(1)*greenVegFrac_monthly
      endif
    endif

   enddo ! looping through domains
  end do ! HRU

  ! compute total area of the upstream HRUS that flow into each HRU
  do iHRU=1,gru_struc(iGRU)%hruCount
   upArea%gru(iGRU)%hru(iHRU) = 0._rkind
   do jHRU=1,gru_struc(iGRU)%hruCount
    ! check if jHRU flows into iHRU; assume no exchange between GRUs
    if(typeStruct%gru(iGRU)%hru(jHRU)%var(iLookTYPE%downHRUindex)==typeStruct%gru(iGRU)%hru(iHRU)%var(iLookID%hruId))then
     upArea%gru(iGRU)%hru(iHRU) = upArea%gru(iGRU)%hru(iHRU) + attrStruct%gru(iGRU)%hru(jHRU)%var(iLookATTR%HRUarea)
    endif   ! (if jHRU is an upstream HRU)
   end do  ! jHRU
  end do  ! iHRU

  ! identify the total basin area for a GRU (m2)
  associate(totalArea => bvarStruct%gru(iGRU)%var(iLookBVAR%basin__totalArea)%dat(1) )
   totalArea = 0._rkind
   do iHRU=1,gru_struc(iGRU)%hruCount
    totalArea = totalArea + attrStruct%gru(iGRU)%hru(iHRU)%var(iLookATTR%HRUarea)
   end do
  end associate

end associate summaVars

end subroutine setupGRU



subroutine f_initGru(indx_gru, handle_gru_data, output_buffer_steps, &
    err, message_r) bind(C, name="f_initGru")
  USE actor_data_types,only:gru_type
  USE data_types,only:var_dlength
  USE globalData,only:statBvar_meta                           ! child metadata for stats
  USE globalData,only:bvar_meta,grid_meta                     ! metadata structures
  USE globalData,only:gru_struc                               ! gru-hru-dom mapping structure
  USE allocspace_module,only:allocLocal,allocGlobal
  USE INIT_HRU_ACTOR,only:initHRU
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  implicit none
  ! Dummy variables
  integer(c_int), intent(in)          :: indx_gru
  type(c_ptr),    intent(in),value    :: handle_gru_data
  integer(c_int), intent(in)          :: output_buffer_steps
  integer(c_int), intent(out)         :: err
  type(c_ptr),   intent(out)          :: message_r

  ! local variables
  type(gru_type),pointer              :: gru_data
  integer(i4b)                        :: iHRU
  character(len=256)                  :: message = ""
  character(len=256)                  :: cmessage

  err = 0; message = "f_initGru/"
  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  ! ****************************************************************************
  ! Initialize our section of the output buffer
  ! ****************************************************************************
  call allocateOutputBuffer(indx_gru, size(gru_data%hru), output_buffer_steps, &
                            err, message)
  if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return;end if

  ! Setup the GRU
  call setupGRU(indx_gru, err, message)
  if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return;end if


  ! ****************************************************************************
  ! Allocate the basin variables
  ! ****************************************************************************
  call allocLocal(bvar_meta,gru_data%bvarStruct,nSnow=0,nLake=0,nSoil=0,nGlce=0,nGlac=gru_struc(indx_gru)%nGlac,err=err,message=cmessage);
  if(err /= 0) then; message=trim(message)//cmessage; call f_c_string_ptr(trim(message), message_r);return;end if
  call allocLocal(statBvar_meta(:)%var_info,gru_data%bvarStat,nSnow=0,nLake=0,nSoil=0,nGlce=0,nGlac=0,err=err,message=cmessage);
  if(err /= 0) then; message=trim(message)//cmessage; call f_c_string_ptr(trim(message), message_r);return;end if

  ! basin glacier grid structure: deep-copy this GRU's grids from init_struc (filled by read_attrb)
  call copyGridStruct(indx_gru, gru_data, err, cmessage)
  if(err /= 0) then; message=trim(message)//cmessage; call f_c_string_ptr(trim(message), message_r);return;end if

  ! ****************************************************************************
  ! Initialize the HRUs
  ! ****************************************************************************
  do iHRU = 1, size(gru_data%hru)
    call initHRU(indx_gru, iHRU, gru_data%hru(iHRU), err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do
end subroutine f_initGru

! deep-copy this GRU's glacier grid(s) from init_struc%gridStruct (populated by read_attrb) into
! the actor GRU handle, so the glacier area-change model has its own working copy of the grid.
subroutine copyGridStruct(indx_gru, gru_data, err, message)
  USE summa_init_struc,only:init_struc
  USE actor_data_types,only:gru_type
  implicit none
  integer(i4b),        intent(in)    :: indx_gru
  type(gru_type),      pointer       :: gru_data
  integer(i4b),        intent(out)   :: err
  character(*),         intent(out)   :: message
  integer(i4b)                       :: iGrid,iVar,nGrid,nVar,nx,ny

  err=0; message="copyGridStruct/"

  if(.not.associated(gru_data%gridStruct)) allocate(gru_data%gridStruct)
  if(allocated(gru_data%gridStruct%grid)) deallocate(gru_data%gridStruct%grid)

  if(.not.allocated(init_struc%gridStruct%gru)) then
    allocate(gru_data%gridStruct%grid(0)); return
  endif
  nGrid = size(init_struc%gridStruct%gru(indx_gru)%grid)
  allocate(gru_data%gridStruct%grid(nGrid))
  do iGrid=1,nGrid
    nVar = size(init_struc%gridStruct%gru(indx_gru)%grid(iGrid)%var)
    allocate(gru_data%gridStruct%grid(iGrid)%var(nVar))
    do iVar=1,nVar
      nx = size(init_struc%gridStruct%gru(indx_gru)%grid(iGrid)%var(iVar)%dat2,1)
      ny = size(init_struc%gridStruct%gru(indx_gru)%grid(iGrid)%var(iVar)%dat2,2)
      allocate(gru_data%gridStruct%grid(iGrid)%var(iVar)%dat2(nx,ny))
      gru_data%gridStruct%grid(iGrid)%var(iVar)%dat2 = &
        init_struc%gridStruct%gru(indx_gru)%grid(iGrid)%var(iVar)%dat2
    end do
  end do
end subroutine copyGridStruct

subroutine setupGRU_fortran(indx_gru, handle_gru_data, err, message_r) &
    bind(C, name="setupGRU_fortran")
  USE summa_init_struc,only:init_struc
  USE actor_data_types,only:gru_type
  USE INIT_HRU_ACTOR,only:setupHRU
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  ! Dummy Variables
  integer(c_int), intent(in)       :: indx_gru
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  ! Local Variables
  integer(i4b)                     :: iHRU
  integer(i4b)                     :: iVar
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  do iHRU = 1, size(gru_data%hru)
    call setupHRU(indx_gru, iHRU, gru_data%hru(iHRU), err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do

  do iVar=1, size(init_struc%bvarStruct%gru(indx_gru)%var(:))
    gru_data%bvarStruct%var(iVar)%dat(:) = init_struc%bvarStruct%gru(indx_gru)%var(iVar)%dat(:)
  enddo
end subroutine setupGRU_fortran

subroutine readGRURestart_fortran(indx_gru, handle_gru_data, err, message_r) &
    bind(C, name="readGRURestart_fortran")
  USE actor_data_types,only:gru_type
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  USE INIT_HRU_ACTOR,only:readHRURestart

  USE var_lookup,only:iLookDECISIONS                          ! look-up values for model decisions
  USE var_lookup,only:iLookBVAR                               ! look-up values for basin-average model variables
  USE globalData,only:model_decisions                         ! model decision structure
  USE mDecisions_module,only:localColumn, & ! separate groundwater representation in each local soil column
                             singleBasin    ! single groundwater store over the entire basin
  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)       :: indx_gru
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  ! Local Variables
  integer(i4b)                     :: iHRU
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  do iHRU = 1, size(gru_data%hru)
    call readHRURestart(indx_gru, iHRU, gru_data%hru(iHRU), err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do

  ! Set the basin variables that pertain to the GRU
  select case(model_decisions(iLookDECISIONS%spatial_gw)%iDecision)
    case(localColumn) 
      gru_data%bvarStruct%var(iLookBVAR%basin__AquiferStorage)%dat(1) = 0._dp
    case(singleBasin)
      gru_data%bvarStruct%var(iLookBVAR%basin__AquiferStorage)%dat(1) = 1._dp
    case default
      message=trim(message)//'unable to identify decision for regional representation of groundwater'
      call f_c_string_ptr(trim(message), message_r)
      err = 1
      return
  end select

end subroutine readGRURestart_fortran

subroutine setTimeZoneOffsetGRU_fortran(iFile, handle_gru_data, err, message_r) & 
    bind(C, name="setTimeZoneOffsetGRU_fortran")
  USE actor_data_types,only:gru_type
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  USE hru_read,only:setTimeZoneOffset
  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)       :: iFile
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  ! Local Variables
  integer(i4b)                     :: iHRU
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  do iHRU = 1, size(gru_data%hru)
    call setTimeZoneOffset(iFile, gru_data%hru(iHRU), err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do

end subroutine setTimeZoneOffsetGRU_fortran

subroutine readGRUForcing_fortran(indx_gru, iStep, iRead, iFile, & 
    handle_gru_data, err, message_r) bind(C, name="readGRUForcing_fortran")
  USE actor_data_types,only:gru_type
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  USE hru_read,only:readHRUForcing
  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)       :: indx_gru
  integer(c_int), intent(in)       :: iStep
  integer(c_int), intent(inout)    :: iRead
  integer(c_int), intent(in)       :: iFile
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  ! Local Variables
  integer(i4b)                     :: iHRU
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  do iHRU = 1, size(gru_data%hru)
    call readHRUForcing(indx_gru, iHRU, iStep, iRead, iFile, &
                        gru_data%hru(iHRU), err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do

end subroutine readGRUForcing_fortran

subroutine runGRU_fortran(indx_gru, modelTimeStep, handle_gru_data, &
    dt_init_factor, err, message_r) bind(C, name="runGRU_fortran")
  USE actor_data_types,only:gru_type
  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  USE, intrinsic :: ieee_arithmetic
  USE summa_modelRun,only:runPhysics
  
  USE globalData,only:model_decisions          ! model decision structure
  USE globalData,only:gru_struc
  USE globalData,only:data_step                ! length of the data step (s)
  USE globalData,only:upland,glacCln1,glacCln2,glacDbr,wetland  ! horizontal domain types
  USE globalData,only:elapsedUpdateArea        ! elapsed time for updating glacier and wetland area for all GRUs (s)
  USE qTimeDelay_module,only:qOverland         ! module to route water through an "unresolved" river network
  USE qTimeDelay_module,only:qGlacier          ! module to route water through the glacier reservoirs
  USE glacAreaChange_module,only:time_updateGlacArea  ! check if glacier area needs to be updated
  USE glacAreaChange_module,only:glacAreaChange       ! change glacier area with ice flow model
  USE glacAreaChange_module,only:updateGlacDomain     ! change glacier domain area, elevation, layering
  USE time_utils_module,only:elapsedSec        ! calculate the elapsed time

  USE mDecisions_module,only:&               ! look-up values for LAI decisions
      monthlyTable,& ! LAI/SAI taken directly from a monthly table for different vegetation classes
      specified,&    ! LAI/SAI computed from green vegetation fraction and winterSAI and summerLAI parameters
      localColumn, & ! separate groundwater representation in each local soil column
      singleBasin, & ! single groundwater store over the entire basin
      bigBucket

  USE var_lookup,only:iLookBVAR              ! look-up values for basin-average model variables
  USE var_lookup,only:iLookBPAR              ! look-up values for basin-average model parameters
  USE var_lookup,only:iLookFLUX              ! look-up values for local column model fluxes
  USE var_lookup,only:iLookDIAG              ! look-up values for local column model diagnostic variables
  USE var_lookup,only:iLookPROG              ! look-up values for local column model prognostic variables
  USE var_lookup,only:iLookATTR              ! look-up values for local attributes
  USE var_lookup,only:iLookDECISIONS         ! look-up values for model decisions
  USE var_lookup,only:iLookTYPE              ! look-up values for HRU types
  USE var_lookup,only:iLookID                ! look-up values for HRU IDs
  USE var_lookup,only:iLookTIME              ! look-up values for model time data
  USE var_lookup,only:iLookPARAM             ! look-up values for model parameters
  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)       :: indx_gru
  integer(c_int), intent(in)       :: modelTimeStep
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(in)       :: dt_init_factor
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  ! Local Variables
  integer(i4b)                     :: iHRU, kHRU, jHRU, iDOM
  integer(i4b)                     :: iVar
  integer(i4b)                     :: typeDOM
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""
  character(len=256)               :: cmessage
  real(rkind)                      :: fracDOM               ! fractional area of a given HRU domain in the GRU (-)
  real(rkind)                      :: glacIceMelt           ! glacier ice reservoir melt (m s-1)
  real(rkind)                      :: glacSnowMelt          ! glacier snow reservoir melt (m s-1)
  real(rkind)                      :: glacFirnMelt          ! glacier firn reservoir melt (m s-1)
  logical(lgt)                     :: hasGlacier            ! flag: the GRU has at least one glacier domain
  ! ----- glacier geometry area-change (ported from run_oneGRU.f90) -------------------------------------------------------
  integer(i4b)                     :: nglacDOM              ! number of glacier domains in the GRU
  integer(i4b)                     :: nglacHRU              ! number of glacier HRUs in the GRU
  integer(i4b)                     :: iglacDOM              ! glacier domain index
  integer(i4b)                     :: iglacHRU              ! glacier HRU index
  integer(i4b)                     :: nSnowD,nLakeD,nSoilD  ! layer counts of the current (debris) domain
  real(rkind)                      :: soil_thick            ! soil(==debris) thickness of a debris domain (m)
  real(rkind)                      :: sec_since_last_update ! seconds since last glacier area update
  logical(lgt)                     :: updateGlacArea        ! flag to update glacier area this time step
  logical(lgt)                     :: updateLakeArea        ! flag to update wetland area this time step
  logical(lgt)                     :: check_updateGlacArea  ! flag to check (once per GRU) if glacier area needs updating
  logical(lgt)                     :: has_glacier           ! flag: the current HRU has at least one glacier domain
  real(rkind)                      :: remaining_area        ! upland residual area (m2)
  real(rkind)                      :: remaining_elev        ! upland residual area-weighted elevation
  real(rkind)                      :: remaining_tan_slope   ! upland residual area-weighted tan slope
  real(rkind)                      :: remaining_aspect_sin  ! upland residual sine component for circular aspect mean
  real(rkind)                      :: remaining_aspect_cos  ! upland residual cosine component for circular aspect mean
  real(rkind),parameter            :: deg2rad=PI_D/180._rkind  ! convert degrees to radians
  real(rkind),parameter            :: rad2deg=180._rkind/PI_D  ! convert radians to degrees
  real(rkind),parameter            :: aspect_tol=1.e-12_rkind  ! tolerance for undefined circular mean
  integer(i4b),dimension(8)        :: startUpdateArea,endUpdateArea  ! wall-clock time around the area update
  real(rkind),allocatable          :: glac_elev(:)             ! elevation of each glacier domain (m)
  real(rkind),allocatable          :: glac_tan_slope(:)        ! tan ground surface slope of each glacier domain (m/m)
  real(rkind),allocatable          :: glac_aspect(:)           ! azimuth of each glacier domain (degrees E of N)
  real(rkind),allocatable          :: glac_contourLength(:)    ! downslope contour length of each glacier domain (m)
  real(rkind),allocatable          :: glac_debris_thick(:)     ! debris thickness of each glacier domain (m)
  real(rkind),allocatable          :: massChange(:)            ! mean water-equivalent change rate since last update (kg m-2 s-1)
  real(rkind),allocatable          :: glac_ablFrac(:)          ! ablation fraction of each glacier domain (-)
  real(rkind),allocatable          :: glac_area(:)             ! area of each glacier domain (m2)
  real(rkind),allocatable          :: iden_soil_mean(:)        ! mean soil(debris) density of each glacier domain (kg m-3)
  real(rkind),allocatable          :: theta_sat_mean(:)        ! mean soil(debris) porosity of each glacier domain (-)
  integer(i8b),allocatable         :: glac_hru(:)              ! HRU index of each glacier domain
  integer(i4b),allocatable         :: nclean(:)                ! number of clean glacier domains in each glacier HRU
  integer(i4b),allocatable         :: ndebris(:)               ! number of debris glacier domains in each glacier HRU

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)

  ! ----- basin initialization --------------------------------------------------------------------------------------------
  gru_data%bvarStruct%var(iLookBVAR%basin__SurfaceRunoff)%dat(1)    = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__SoilDrainage)%dat(1)     = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__ColumnOutflow)%dat(1)    = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__TotalRunoff)%dat(1)      = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__AquiferRecharge)%dat(1)  = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__AquiferBaseflow)%dat(1)  = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__AquiferTranspire)%dat(1) = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__StorageChange)%dat(1)    = 0._dp
  gru_data%bvarStruct%var(iLookBVAR%basin__GlacierArea)%dat(1)      = 0._dp
  glacIceMelt = 0._rkind; glacSnowMelt = 0._rkind; glacFirnMelt = 0._rkind
  hasGlacier = .false.
  updateGlacArea = .false.
  updateLakeArea = .false.
  sec_since_last_update = 0._rkind
  nglacDOM = 0
  nglacHRU = 0

  ! ----- zero lateral inflow and decide (once) whether glacier geometry is updated this step ---------------------------
  check_updateGlacArea = .true.
  do iHRU = 1, size(gru_data%hru)
    do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
      typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
      gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%mLayerColumnInflow)%dat(:) = 0._rkind
      if(gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMarea)%dat(1)==0._rkind) cycle
      if(typeDOM==glacCln1 .or. typeDOM==glacCln2 .or. typeDOM==glacDbr)then
        if(check_updateGlacArea)then
          call time_updateGlacArea( &
              gru_data%hru(iHRU)%timeStruct%var(iLookTIME%iyyy), gru_data%hru(iHRU)%timeStruct%var(iLookTIME%im),   &
              gru_data%hru(iHRU)%timeStruct%var(iLookTIME%id),   gru_data%hru(iHRU)%timeStruct%var(iLookTIME%ih),   &
              gru_data%hru(iHRU)%timeStruct%var(iLookTIME%imin),                                                    &
              gru_data%hru(iHRU)%attrStruct%var(iLookATTR%latitude),                                                &
              gru_data%bvarStruct%var(iLookBVAR%updateJulDay)%dat(1),                                               &
              gru_data%bvarStruct%var(iLookBVAR%updateJulDayNext)%dat(1),                                           &
              updateGlacArea, sec_since_last_update, err, cmessage)
          if(err/=0)then; err=30; message=trim(message)//trim(cmessage); call f_c_string_ptr(trim(message), message_r); return; endif
          check_updateGlacArea = .false.
        endif
      endif
    end do
  end do

  ! ----- on an update year, count glacier domains / HRUs and allocate the area-change work arrays ---------------------
  if(updateGlacArea)then
    do iHRU = 1, size(gru_data%hru)
      has_glacier = .false.
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
        if(typeDOM==glacCln1 .or. typeDOM==glacCln2 .or. typeDOM==glacDbr)then
          nglacDOM = nglacDOM + 1
          if(.not.has_glacier)then; has_glacier = .true.; nglacHRU = nglacHRU + 1; endif
        endif
      end do
    end do
    allocate(glac_elev(nglacDOM),glac_debris_thick(nglacDOM),glac_area(nglacDOM),glac_ablFrac(nglacDOM), &
             massChange(nglacDOM),glac_hru(nglacDOM),iden_soil_mean(nglacDOM),theta_sat_mean(nglacDOM),  &
             nclean(nglacHRU),ndebris(nglacHRU),glac_tan_slope(nglacDOM),glac_aspect(nglacDOM),          &
             glac_contourLength(nglacDOM),stat=err)
    if(err/=0)then; err=20; message=trim(message)//'problem allocating glacier area-change work arrays'; call f_c_string_ptr(trim(message), message_r); return; endif
  endif

  do iHRU = 1, size(gru_data%hru)
    ! Give the HRU the up to date basin variables
    do iVar=1, size(gru_data%bvarStruct%var(:))
      gru_data%hru(iHRU)%bvarStruct%var(iVar)%dat(:) = gru_data%bvarStruct%var(iVar)%dat(:)
    end do

    call runPhysics(indx_gru, iHRU, modelTimeStep, gru_data%hru(iHRU), &
                    dt_init_factor, err, message)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if

    ! identify the downslope HRU (lateral connectivity)
    kHRU = 0
    dsHRU: do jHRU=1,gru_struc(indx_gru)%hruCount
      if(gru_data%hru(iHRU)%typeStruct%var(iLookTYPE%downHRUindex) == gru_data%hru(jHRU)%idStruct%var(iLookID%hruId))then
        if(kHRU==0)then; kHRU=jHRU; exit dsHRU; end if
      end if
    end do dsHRU

    ! ----- aggregate weighted GRU fluxes over each domain within the HRU --------------------------------------------------
    do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
      typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
      if(typeDOM==wetland)then
        err=20; message=trim(message)//'ERROR: wetland fluxes not yet implemented'
        call f_c_string_ptr(trim(message), message_r); return
      endif
      associate(DOMarea => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMarea)%dat(1))
      if(DOMarea>0._rkind)then
      fracDOM = DOMarea / gru_data%hru(iHRU)%bvarStruct%var(iLookBVAR%basin__totalArea)%dat(1)

      ! total mass storage change (kg m-2 s-1)
      gru_data%bvarStruct%var(iLookBVAR%basin__StorageChange)%dat(1) = &
          gru_data%bvarStruct%var(iLookBVAR%basin__StorageChange)%dat(1) + &
          gru_data%hru(iHRU)%diagStruct%dom(iDOM)%var(iLookDIAG%scalarTotalMassChange)%dat(1)*fracDOM

      if(typeDOM==upland)then
        ! lateral flow: add outflow to the downslope HRU's upland domain, else to GRU column outflow
        if(kHRU > 0)then
          gru_data%hru(kHRU)%fluxStruct%dom(1)%var(iLookFLUX%mLayerColumnInflow)%dat(:) = &
              gru_data%hru(kHRU)%fluxStruct%dom(1)%var(iLookFLUX%mLayerColumnInflow)%dat(:) + &
              gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%mLayerColumnOutflow)%dat(:)
        else
          gru_data%bvarStruct%var(iLookBVAR%basin__ColumnOutflow)%dat(1) = &
              gru_data%bvarStruct%var(iLookBVAR%basin__ColumnOutflow)%dat(1) + &
              sum(gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%mLayerColumnOutflow)%dat(:))
        end if

        gru_data%bvarStruct%var(iLookBVAR%basin__SurfaceRunoff)%dat(1) = &
            gru_data%bvarStruct%var(iLookBVAR%basin__SurfaceRunoff)%dat(1) + &
            gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarSurfaceRunoff)%dat(1)*fracDOM
        gru_data%bvarStruct%var(iLookBVAR%basin__SoilDrainage)%dat(1) = &
            gru_data%bvarStruct%var(iLookBVAR%basin__SoilDrainage)%dat(1) + &
            gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarSoilDrainage)%dat(1)*fracDOM

        if(model_decisions(iLookDECISIONS%spatial_gw)%iDecision == localColumn .and. &
           model_decisions(iLookDECISIONS%groundwatr)%iDecision == bigBucket) then
          gru_data%bvarStruct%var(iLookBVAR%basin__AquiferRecharge)%dat(1)  = &
              gru_data%bvarStruct%var(iLookBVAR%basin__AquiferRecharge)%dat(1) + &
              gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarAquiferRecharge)%dat(1)*fracDOM
          gru_data%bvarStruct%var(iLookBVAR%basin__AquiferTranspire)%dat(1) = &
              gru_data%bvarStruct%var(iLookBVAR%basin__AquiferTranspire)%dat(1) + &
              gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarAquiferTranspire)%dat(1)*fracDOM
          gru_data%bvarStruct%var(iLookBVAR%basin__AquiferBaseflow)%dat(1) = &
              gru_data%bvarStruct%var(iLookBVAR%basin__AquiferBaseflow)%dat(1) + &
              gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarAquiferBaseflow)%dat(1)*fracDOM
        end if

      else if(typeDOM==glacCln1 .or. typeDOM==glacCln2 .or. typeDOM==glacDbr)then
        hasGlacier = .true.
        associate(ablFrac  => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%scalarAblFrac)%dat(1), &
                  snowDepth=> gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%scalarSnowDepth)%dat(1), &
                  glacMelt => gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%scalarGlacierMelt)%dat(1), &
                  colOut   => sum(gru_data%hru(iHRU)%fluxStruct%dom(iDOM)%var(iLookFLUX%mLayerColumnOutflow)%dat(:)))
          glacFirnMelt = glacFirnMelt + glacMelt*fracDOM*(1.0_rkind - ablFrac)
          if(snowDepth>0._rkind)then
            glacSnowMelt = glacSnowMelt + (glacMelt + colOut/gru_data%hru(iHRU)%bvarStruct%var(iLookBVAR%basin__totalArea)%dat(1))*fracDOM*ablFrac
          else
            glacIceMelt  = glacIceMelt  + (glacMelt + colOut/gru_data%hru(iHRU)%bvarStruct%var(iLookBVAR%basin__totalArea)%dat(1))*fracDOM*ablFrac
          endif
        end associate
        gru_data%bvarStruct%var(iLookBVAR%basin__GlacierArea)%dat(1) = &
            gru_data%bvarStruct%var(iLookBVAR%basin__GlacierArea)%dat(1) + DOMarea
        gru_data%bvarStruct%var(iLookBVAR%basin__GlacierStorage)%dat(1) = &
            gru_data%bvarStruct%var(iLookBVAR%basin__GlacierStorage)%dat(1) + &
            gru_data%hru(iHRU)%diagStruct%dom(iDOM)%var(iLookDIAG%scalarTotalMassChange)%dat(1)*data_step*DOMarea*1.e-12_rkind
      endif ! (domain type)
      end if ! (DOMarea > 0)
      end associate
    end do ! iDOM
  end do
  ! ***********************************************************************************************************************
  ! ********** END LOOP THROUGH HRUS **************************************************************************************
  ! ***********************************************************************************************************************
  ! if a year has passed since the last glacier-area update, gather the per-glacier-domain geometry and mass change
  if(updateGlacArea)then
    iglacHRU = 0; iglacDOM = 0
    iden_soil_mean = 0._rkind; theta_sat_mean = 0._rkind
    nclean = 0; ndebris = 0
    do iHRU = 1, size(gru_data%hru)
      has_glacier = .false.
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
        if(typeDOM/=glacCln1 .and. typeDOM/=glacCln2 .and. typeDOM/=glacDbr) cycle
        associate(DOMarea          => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMarea)%dat(1),          &
                  DOMelev          => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMelev)%dat(1),          &
                  DOMtan_slope     => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMtan_slope)%dat(1),     &
                  DOMaspect        => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMaspect)%dat(1),        &
                  DOMcontourLength => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMcontourLength)%dat(1), &
                  mLayerDepth      => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%mLayerDepth)%dat)
          iglacDOM = iglacDOM + 1
          glac_hru(iglacDOM) = iHRU
          if(.not.has_glacier)then; has_glacier = .true.; iglacHRU = iglacHRU + 1; endif
          if(typeDOM==glacCln1 .or. typeDOM==glacCln2) nclean(iglacHRU)  = nclean(iglacHRU)  + 1
          if(typeDOM==glacDbr)                         ndebris(iglacHRU) = ndebris(iglacHRU) + 1
          if(DOMarea>0._rkind)then
            nSnowD = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSnow
            nLakeD = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nLake
            nSoilD = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSoil
            glac_elev(iglacDOM)          = DOMelev
            glac_area(iglacDOM)          = DOMarea
            glac_tan_slope(iglacDOM)     = DOMtan_slope
            glac_aspect(iglacDOM)        = DOMaspect
            glac_contourLength(iglacDOM) = DOMcontourLength
            massChange(iglacDOM)         = gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%glacMass4AreaChange)%dat(1)
            if(typeDOM==glacDbr)then
              soil_thick = sum(mLayerDepth(nSnowD+nLakeD+1:nSnowD+nLakeD+nSoilD))
              iden_soil_mean(iglacDOM) = iden_soil_mean(iglacDOM) + &
                  sum(gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%soil_dens_intr)%dat(1:nSoilD) &
                      *mLayerDepth(nSnowD+nLakeD+1:nSnowD+nLakeD+nSoilD)) / soil_thick
              theta_sat_mean(iglacDOM) = theta_sat_mean(iglacDOM) + &
                  sum(gru_data%hru(iHRU)%mparStruct%dom(iDOM)%var(iLookPARAM%theta_sat)%dat(1:nSoilD) &
                      *mLayerDepth(nSnowD+nLakeD+1:nSnowD+nLakeD+nSoilD)) / soil_thick
              glac_debris_thick(iglacDOM) = soil_thick
            else
              glac_debris_thick(iglacDOM) = 0._rkind
            endif
          else
            glac_elev(iglacDOM)          = realMissing
            glac_tan_slope(iglacDOM)     = realMissing
            glac_aspect(iglacDOM)        = realMissing
            glac_contourLength(iglacDOM) = 0._rkind
            glac_area(iglacDOM)          = 0._rkind
            massChange(iglacDOM)         = 0._rkind
            glac_debris_thick(iglacDOM)  = 0._rkind
            iden_soil_mean(iglacDOM)     = 0._rkind
            theta_sat_mean(iglacDOM)     = 0._rkind
          endif
        end associate
      end do
    end do
  endif

  ! lapse glacier melt to the basin by routing through each glacier reservoir
  if(hasGlacier)then
    call qGlacier(&
        gru_data%hru(1)%bparStruct%var(iLookBPAR%glacStor_kIce),          &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%glacStor_kFirn),         &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%glacStor_kFirn),         &
        glacIceMelt, glacSnowMelt, glacFirnMelt,                         &
        gru_data%bvarStruct%var(iLookBVAR%glacierAblArea)%dat,           &
        gru_data%bvarStruct%var(iLookBVAR%glacierAccArea)%dat,           &
        gru_struc(indx_gru)%nGlac,                                       &
        gru_data%bvarStruct%var(iLookBVAR%glacIceRunoffFuture)%dat,      &
        gru_data%bvarStruct%var(iLookBVAR%glacSnowRunoffFuture)%dat,     &
        gru_data%bvarStruct%var(iLookBVAR%glacFirnRunoffFuture)%dat,     &
        gru_data%bvarStruct%var(iLookBVAR%glacierRoutedRunoff)%dat(1),   &
        err,cmessage)
    if(err/=0)then; err=20; message=trim(message)//trim(cmessage); call f_c_string_ptr(trim(message), message_r); return; endif
  else
    gru_data%bvarStruct%var(iLookBVAR%glacierRoutedRunoff)%dat(1) = 0._rkind
  endif

  ! perform the overland routing
  associate(totalArea => gru_data%bvarStruct%var(iLookBVAR%basin__totalArea)%dat(1) )

  if(model_decisions(iLookDECISIONS%spatial_gw)%iDecision == singleBasin)then
    message=trim(message)//'multi_driver/bigBucket groundwater code not transferred from old code base yet'
    err=20; call f_c_string_ptr(trim(message), message_r); return
  end if

  if(model_decisions(iLookDECISIONS%groundwatr)%iDecision == bigBucket) then
    gru_data%bvarStruct%var(iLookBVAR%basin__TotalRunoff)%dat(1) = &
        gru_data%bvarStruct%var(iLookBVAR%basin__SurfaceRunoff)%dat(1) + &
        gru_data%bvarStruct%var(iLookBVAR%basin__ColumnOutflow)%dat(1)/totalArea + &
        gru_data%bvarStruct%var(iLookBVAR%basin__AquiferBaseflow)%dat(1)
  else
    gru_data%bvarStruct%var(iLookBVAR%basin__TotalRunoff)%dat(1) = &
        gru_data%bvarStruct%var(iLookBVAR%basin__SurfaceRunoff)%dat(1) + &
        gru_data%bvarStruct%var(iLookBVAR%basin__ColumnOutflow)%dat(1)/totalArea + &
        gru_data%bvarStruct%var(iLookBVAR%basin__SoilDrainage)%dat(1)
  endif

  call qOverland(&
      model_decisions(iLookDECISIONS%subRouting)%iDecision,            &
      gru_data%bvarStruct%var(iLookBVAR%basin__TotalRunoff)%dat(1),    &
      gru_data%bvarStruct%var(iLookBVAR%routingFractionFuture)%dat,    &
      gru_data%bvarStruct%var(iLookBVAR%routingRunoffFuture)%dat,      &
      gru_data%bvarStruct%var(iLookBVAR%averageInstantRunoff)%dat(1),  &
      gru_data%bvarStruct%var(iLookBVAR%averageRoutedRunoff)%dat(1),   &
      err,message)
  if(err/=0)then; err=20; message=trim(message)//trim(cmessage); print*, message; call f_c_string_ptr(trim(message), message_r); return; endif;

  ! add glacier runoff to the overland runoff
  gru_data%bvarStruct%var(iLookBVAR%averageInstantRunoff)%dat(1) = &
      gru_data%bvarStruct%var(iLookBVAR%averageInstantRunoff)%dat(1) + glacIceMelt + glacSnowMelt + glacFirnMelt
  gru_data%bvarStruct%var(iLookBVAR%averageRoutedRunoff)%dat(1) = &
      gru_data%bvarStruct%var(iLookBVAR%averageRoutedRunoff)%dat(1) + gru_data%bvarStruct%var(iLookBVAR%glacierRoutedRunoff)%dat(1)
  end associate

  ! ----- update the glacier geometry (ice-flow area change + domain re-layering) ---------------------------------------
  call date_and_time(values=startUpdateArea)
  if(updateGlacArea)then
    call glacAreaChange( &
        sec_since_last_update, nglacHRU, nglacDOM, ndebris, nclean, glac_hru,              &
        gru_struc(indx_gru)%nGlac, gru_struc(indx_gru)%glacInfo, gru_struc(indx_gru)%gridInfo, &
        gru_data%gridStruct,                                                               &
        massChange, glac_elev, glac_tan_slope, glac_aspect, glac_contourLength,            &
        glac_debris_thick, iden_soil_mean, theta_sat_mean,                                 &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%debrisConc),                              &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%wallErosionRate),                         &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%debrisCritStress),                        &
        gru_data%hru(1)%bparStruct%var(iLookBPAR%latMoraineWidth),                         &
        gru_data%bvarStruct%var(iLookBVAR%glacierAblArea)%dat,                             &
        gru_data%bvarStruct%var(iLookBVAR%glacierAccArea)%dat,                             &
        glac_area, glac_ablFrac, err, cmessage)
    if(err/=0)then; err=20; message=trim(message)//trim(cmessage); call f_c_string_ptr(trim(message), message_r); return; endif

    ! push the new geometry / layering back into each glacier domain
    iglacDOM = 0
    do iHRU = 1, size(gru_data%hru)
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
        if(typeDOM/=glacCln1 .and. typeDOM/=glacCln2 .and. typeDOM/=glacDbr) cycle
        iglacDOM = iglacDOM + 1
        call updateGlacDomain( &
            iglacDOM, glac_elev, glac_area, glac_tan_slope, glac_aspect, glac_contourLength, &
            glac_ablFrac, glac_debris_thick, typeDOM,                                        &
            gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSnow,                           &
            gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nLake,                           &
            gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSoil,                           &
            gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nGlce,                           &
            gru_data%hru(iHRU)%mparStruct%dom(iDOM), gru_data%hru(iHRU)%indxStruct%dom(iDOM), &
            gru_data%hru(iHRU)%progStruct%dom(iDOM), gru_data%hru(iHRU)%diagStruct%dom(iDOM), &
            gru_data%hru(iHRU)%fluxStruct%dom(iDOM), err, cmessage)
        if(err/=0)then; err=20; message=trim(message)//trim(cmessage); call f_c_string_ptr(trim(message), message_r); return; endif
      end do
    end do
    deallocate(glac_elev,glac_debris_thick,glac_area,glac_ablFrac,massChange,glac_hru,iden_soil_mean, &
               theta_sat_mean,nclean,ndebris,glac_tan_slope,glac_aspect,glac_contourLength)
  endif

  ! recompute the upland domain residual coordinates from the updated non-upland areas
  if(updateGlacArea .or. updateLakeArea)then
    do iHRU = 1, size(gru_data%hru)
      associate(HRUarea => gru_data%hru(iHRU)%attrStruct%var(iLookATTR%HRUarea),     &
                HRUelev => gru_data%hru(iHRU)%attrStruct%var(iLookATTR%elevation),   &
                HRUslp  => gru_data%hru(iHRU)%attrStruct%var(iLookATTR%tan_slope),   &
                HRUasp  => gru_data%hru(iHRU)%attrStruct%var(iLookATTR%aspect))
        remaining_area       = HRUarea
        remaining_elev       = HRUarea*HRUelev
        remaining_tan_slope  = HRUarea*HRUslp
        remaining_aspect_sin = HRUarea*sin(HRUasp*deg2rad)
        remaining_aspect_cos = HRUarea*cos(HRUasp*deg2rad)
      end associate
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
        associate(DOMarea      => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMarea)%dat(1),      &
                  DOMelev      => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMelev)%dat(1),      &
                  DOMtan_slope => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMtan_slope)%dat(1), &
                  DOMaspect    => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMaspect)%dat(1))
          if(typeDOM/=upland .and. DOMarea>0._rkind)then
            remaining_area       = remaining_area       - DOMarea
            remaining_elev       = remaining_elev       - DOMarea*DOMelev
            remaining_tan_slope  = remaining_tan_slope  - DOMarea*DOMtan_slope
            remaining_aspect_sin = remaining_aspect_sin - DOMarea*sin(DOMaspect*deg2rad)
            remaining_aspect_cos = remaining_aspect_cos - DOMarea*cos(DOMaspect*deg2rad)
          endif
        end associate
      end do
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        typeDOM = gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%dom_type
        if(typeDOM/=upland) cycle
        associate(DOMarea          => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMarea)%dat(1),      &
                  DOMelev          => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMelev)%dat(1),      &
                  DOMtan_slope     => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMtan_slope)%dat(1), &
                  DOMaspect        => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMaspect)%dat(1),    &
                  DOMcontourLength => gru_data%hru(iHRU)%progStruct%dom(iDOM)%var(iLookPROG%DOMcontourLength)%dat(1))
          DOMarea = remaining_area
          if(remaining_area>0._rkind)then
            DOMelev      = remaining_elev/remaining_area
            DOMtan_slope = remaining_tan_slope/remaining_area
            if(remaining_aspect_sin**2 + remaining_aspect_cos**2 > aspect_tol)then
              DOMaspect = modulo(atan2(remaining_aspect_sin,remaining_aspect_cos)*rad2deg,360._rkind)
            else
              DOMaspect = 0._rkind
            endif
          else
            DOMelev = realMissing; DOMarea = 0._rkind
            DOMtan_slope = realMissing; DOMaspect = realMissing
            DOMcontourLength = 0._rkind
          endif
        end associate
      end do
    end do
  endif
  call date_and_time(values=endUpdateArea)
  elapsedUpdateArea = elapsedUpdateArea + elapsedSec(startUpdateArea,endUpdateArea)

  ! update hru's bvarStruct with the basin's bvarStruct
  do iHRU = 1, size(gru_data%hru)
    do iVar=1, size(gru_data%bvarStruct%var(:))
      gru_data%hru(iHRU)%bvarStruct%var(iVar)%dat(:) = gru_data%bvarStruct%var(iVar)%dat(:)
    end do
  end do

end subroutine runGRU_fortran

subroutine writeGRUOutput_fortran(indx_gru, timestep, outputstep, &
    handle_gru_data, err, message_r, year, month, day, hour) bind(C, name="writeGRUOutput_fortran")
  USE actor_data_types,only:gru_type
  USE HRUwriteoOutput_module,only:writeHRUOutput, hru_writeRestart
  USE var_lookup,only:iLookTIME                 ! named variables for time data structure

  USE C_interface_module,only:f_c_string_ptr  ! convert fortran string to c string
  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)       :: indx_gru
  integer(c_int), intent(in)       :: timestep
  integer(c_int), intent(in)       :: outputstep
  type(c_ptr),    intent(in),value :: handle_gru_data
  integer(c_int), intent(out)      :: err
  type(c_ptr),    intent(out)      :: message_r
  integer(c_int), intent(out) :: year, month, day, hour
  ! Local Variables
  integer(i4b)                     :: iHRU
  type(gru_type),pointer           :: gru_data
  character(len=256)               :: message = ""

  call f_c_string_ptr(trim(message), message_r)
  call c_f_pointer(handle_gru_data, gru_data)
  year = gru_data%hru(1)%timeStruct%var(iLookTIME%iyyy)
  month = gru_data%hru(1)%timeStruct%var(iLookTIME%im)
  day = gru_data%hru(1)%timeStruct%var(iLookTIME%id)
  hour = gru_data%hru(1)%timeStruct%var(iLookTIME%ih)

  do iHRU = 1, size(gru_data%hru)
    call writeHRUOutput(indx_gru, iHRU, timestep, outputstep, gru_data%hru(iHRU), & 
                        err, message)
    call hru_writeRestart(indx_gru, iHRU, timestep, outputstep, gru_data%hru(iHRU), &
                         err)
    if(err /= 0) then; call f_c_string_ptr(trim(message), message_r);return; end if
  end do

end subroutine writeGRUOutput_fortran

! Local Subroutines
subroutine allocateOutputBuffer(indx_gru, num_hru, output_buffer_steps, &
    err, message)
  USE output_buffer,only:summa_struct
  USE globalData,only:structInfo                                ! information on the data structures
  USE allocspace_module,only:allocLocal                         ! module to allocate space for global data structures
  USE globalData,only:gru_struc                                 ! information on the GRUs
  
  USE globalData,only:time_meta,forc_meta,attr_meta,type_meta   ! metadata structures
  USE globalData,only:prog_meta,diag_meta,flux_meta,id_meta     ! metadata structures
  USE globalData,only:mpar_meta,indx_meta                       ! metadata structures
  USE globalData,only:bpar_meta,bvar_meta                       ! metadata structures

  USE globalData,only:statForc_meta,statProg_meta,statDiag_meta ! child metadata for stats
  USE globalData,only:statFlux_meta,statIndx_meta,statBvar_meta ! child metadata for stats
  USE globalData,only:lookup_meta                               ! child metadata for stats
  USE globalData,only:maxSnowLayers
  USE globalData,only:maxLakeLayers
  USE globalData,only:maxSoilLayers
  USE globalData,only:maxGlceLayers
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  

  implicit none
  ! Dummy Variables
  integer(c_int), intent(in)        :: indx_gru
  integer(c_int), intent(in)        :: num_hru
  integer(c_int), intent(in)        :: output_buffer_steps
  integer(c_int), intent(out)       :: err 
  character(len=256), intent(out)   :: message
  ! Local Variables
  integer(i4b)                      :: iHRU
  integer(i4b)                      :: iDOM
  integer(i4b)                      :: iStep
  integer(i4b)                      :: iStruct
  integer(i4b)                      :: iDat
  integer(i4b)                      :: domCount

  if (allocated(summa_struct(1)%timeStruct%gru(indx_gru)%hru)) then
    return
  endif

  allocate(summa_struct(1)%forcStat%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%progStat%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%diagStat%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%fluxStat%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%indxStat%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%bvarStat%gru(indx_gru)%hru(num_hru))
  ! Primary Data Structures (scalars)
  allocate(summa_struct(1)%timeStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%forcStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%attrStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%typeStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%idStruct%gru(indx_gru)%hru(num_hru))
  ! Primary Data Structures (variable length vectors)
  allocate(summa_struct(1)%indxStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%mparStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%progStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%diagStruct%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%fluxStruct%gru(indx_gru)%hru(num_hru))
  ! Basin-Average structures
  allocate(summa_struct(1)%bvarStruct%gru(indx_gru)%hru(num_hru))
  ! Finalize Stats for writing
  allocate(summa_struct(1)%finalizeStats%gru(indx_gru)%hru(num_hru))
  allocate(summa_struct(1)%dparStruct%gru(indx_gru)%hru(num_hru))

  ! allocate the per-domain container for every structure that carries a domain dimension
  do iHRU=1,num_hru
    domCount = gru_struc(indx_gru)%hruInfo(iHRU)%domCount
    allocate(summa_struct(1)%progStat%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%diagStat%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%fluxStat%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%indxStat%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%mparStruct%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%progStruct%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%diagStruct%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%fluxStruct%gru(indx_gru)%hru(iHRU)%dom(domCount))
    allocate(summa_struct(1)%indxStruct%gru(indx_gru)%hru(iHRU)%dom(domCount))
  end do

  call allocLocal(bpar_meta,summa_struct(1)%bparStruct%gru(indx_gru), &
                  nSnow=0,nLake=0,nSoil=0,nGlce=0,nGlac=0,err=err,message=message);
  do iHRU=1,num_hru
      ! HRU-level structures (no domain dimension) -- use the upland domain layer counts
      associate(nSnow => gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(1)%nSnow, &
                nSoil => gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(1)%nSoil)
      do iStruct=1,size(structInfo)
        select case(trim(structInfo(iStruct)%structName))
        case('time')
          call alloc_outputStruc(time_meta,summa_struct(1)%timeStruct%gru(indx_gru)%hru(iHRU), &
                                      nSteps=output_buffer_steps,err=err,message=message)
        case('forc')
          call alloc_outputStruc(forc_meta,summa_struct(1)%forcStruct%gru(indx_gru)%hru(iHRU), &
                                 nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message)
          call alloc_outputStruc(statForc_meta(:)%var_info,summa_struct(1)%forcStat%gru(indx_gru)%hru(iHRU), &
                                 nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message);
        case('attr')
          call allocLocal(attr_meta,summa_struct(1)%attrStruct%gru(indx_gru)%hru(iHRU),nSnow,0,nSoil,0,0,err,message)
        case('type')
          call allocLocal(type_meta,summa_struct(1)%typeStruct%gru(indx_gru)%hru(iHRU),nSnow,0,nSoil,0,0,err,message)
        case('id'  )
          call allocLocal(id_meta,  summa_struct(1)%idStruct%gru(indx_gru)%hru(iHRU),nSnow,0,nSoil,0,0,err,message)
        case('bvar')
          call alloc_outputStruc(bvar_meta,summa_struct(1)%bvarStruct%gru(indx_gru)%hru(iHRU), &
                                 nSteps=output_buffer_steps,nSnow=0,nLake=0,nSoil=0,nGlce=0,str_name='bvar',err=err,message=message);
          call alloc_outputStruc(statBvar_meta(:)%var_info,summa_struct(1)%bvarStat%gru(indx_gru)%hru(iHRU), &
                                 nSteps=output_buffer_steps,nSnow=0,nLake=0,nSoil=0,nGlce=0,str_name='bvar',err=err,message=message);
        end select
      end do
      end associate

      ! per-domain structures
      do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
        call alloc_outputStruc(indx_meta,summa_struct(1)%indxStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,str_name='indx',err=err,message=message);
        call alloc_outputStruc(statIndx_meta(:)%var_info,summa_struct(1)%indxStat%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,str_name='indx',err=err,message=message);
        call alloc_outputStruc(prog_meta,summa_struct(1)%progStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,str_name='prog',err=err,message=message);
        call alloc_outputStruc(statProg_meta(:)%var_info,summa_struct(1)%progStat%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,str_name='prog',err=err,message=message);
        call alloc_outputStruc(diag_meta,summa_struct(1)%diagStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message);
        call alloc_outputStruc(statDiag_meta(:)%var_info,summa_struct(1)%diagStat%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message);
        call alloc_outputStruc(flux_meta,summa_struct(1)%fluxStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message);
        call alloc_outputStruc(statFlux_meta(:)%var_info,summa_struct(1)%fluxStat%gru(indx_gru)%hru(iHRU)%dom(iDOM), &
                               nSteps=output_buffer_steps,nSnow=maxSnowLayers,nLake=maxLakeLayers,nSoil=maxSoilLayers,nGlce=maxGlceLayers,err=err,message=message);
        call allocLocal(mpar_meta,summa_struct(1)%mparStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM),maxSnowLayers,maxLakeLayers,maxSoilLayers,maxGlceLayers,0,err,message)
      end do
      ! default model parameters (HRU level in the output buffer)
      call allocLocal(mpar_meta,summa_struct(1)%dparStruct%gru(indx_gru)%hru(iHRU),maxSnowLayers,maxLakeLayers,maxSoilLayers,maxGlceLayers,0,err,message)

      ! Finalize Stats Structure
      allocate(summa_struct(1)%finalizeStats%gru(indx_gru)%hru(iHRU)%tim(output_buffer_steps))
      do iStep = 1, output_buffer_steps
        allocate(summa_struct(1)%finalizeStats%gru(indx_gru)%hru(iHRU)%tim(iStep)%dat(1:maxvarFreq))
        summa_struct(1)%finalizeStats%gru(indx_gru)%hru(iHRU)%tim(iStep)%dat(:) = .false.
      end do ! timeSteps
  end do
end subroutine allocateOutputBuffer

subroutine alloc_outputStruc(metaStruct,dataStruct,nSteps,nSnow,nLake,nSoil,nGlce,str_name,err,message)
  USE data_types
  USE actor_data_types
  USE var_lookup,only:iLookINDEX
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  implicit none
  type(var_info),intent(in)            :: metaStruct(:)
  class(*),intent(inout)               :: dataStruct
  ! optional input
  integer(i4b),intent(in),optional     :: nSteps
  integer(i4b),intent(in),optional     :: nSnow          ! number of snow layers
  integer(i4b),intent(in),optional     :: nLake          ! number of lake layers
  integer(i4b),intent(in),optional     :: nSoil          ! number of soil layers
  integer(i4b),intent(in),optional     :: nGlce          ! number of glacier ice layers
  character(len=*),intent(in),optional :: str_name    ! name of the structure to allocate
  ! output
  integer(i4b),intent(inout)           :: err            ! error code
  character(*),intent(out)             :: message        ! error message
  ! local
  logical(lgt)                         :: check          ! .true. if the variables are allocated
  logical(lgt)                         :: allocAllFlag   ! .true. if struct is to have all timesteps allocated
  integer(i4b)                         :: nVars          ! number of variables in the metadata structure
  integer(i4b)                         :: nLayers        ! total number of layers
  integer(i4b)                         :: nLake_l,nGlce_l ! local copies of the lake/glce layer counts
  integer(i4b)                         :: iVar
  integer(i4b)                         :: iStat          ! checks if we want this variable
  character(len=256)                   :: cmessage       ! error message of the downwind routine
  ! initalize error control
  message='alloc_outputStruc'

  allocAllFlag = .false.
  if (present(str_name)) then
    allocAllFlag = .true.
  end if
  nLake_l = 0; if(present(nLake)) nLake_l = nLake
  nGlce_l = 0; if(present(nGlce)) nGlce_l = nGlce

  nVars = size(metaStruct)
  if(present(nSnow) .or. present(nSoil))then
    ! check both are present
    if(.not.present(nSoil))then; err=20; message=trim(message)//'expect nSoil to be present when nSnow is present'; print*,message; return; end if
    if(.not.present(nSnow))then; err=20; message=trim(message)//'expect nSnow to be present when nSoil is present'; print*,message; return; end if
    nLayers = nSnow+nLake_l+nSoil+nGlce_l
    ! It is possible that nSnow and nSoil are actually needed here, so we return an error if the optional arguments are missing when needed
  else
    select type(dataStruct)
      class is (var_time_ilength); err=20
      class is (var_time_dlength); err=20
    end select
    if(err/=0)then; message=trim(message)//'expect nSnow and nSoil to be present for variable-length data structures'; print*,message; return; end if
  end if

  check=.false.
  ! allocate the space for the variables and thier time steps in the output structure
  select type(dataStruct)
    ! ****************************************************
    class is (var_time_i)
      if(allocated(dataStruct%var))then
        check=.true.
      else 
        allocate(dataStruct%var(nVars),stat=err)
      end if
      do iVar=1, nVars
        ! Check if this variable is desired within any timeframe
        if(is_var_desired(metaStruct,iVar) .or. allocAllFlag)then
          allocate(dataStruct%var(iVar)%tim(nSteps))
        end if
      end do
      return
    ! ****************************************************
    class is (var_time_i8)
      if(allocated(dataStruct%var))then 
        check=.true.
      else 
        allocate(dataStruct%var(nVars),stat=err) 
      end if 
      do iVar=1, nVars
        ! Check if this variable is desired within any timeframe
        if(is_var_desired(metaStruct,iVar) .or. allocAllFlag)then
          allocate(dataStruct%var(iVar)%tim(nSteps))
        end if
      end do
      return
    ! ****************************************************
    class is (var_time_d)
      if(allocated(dataStruct%var))then
        check=.true.
      else
        allocate(dataStruct%var(nVars),stat=err)
      end if
      do iVar=1, nVars
        ! Check if this variable is desired within any timeframe
        if(is_var_desired(metaStruct,iVar) .or. allocAllFlag)then
          allocate(dataStruct%var(iVar)%tim(nSteps))
        end if
      end do
      return
    ! ****************************************************   
    class is (var_d)
      if(allocated(dataStruct%var))then
        check=.true.
      else
        allocate(dataStruct%var(nVars),stat=err)
      end if
      return
    ! ****************************************************
    class is (var_i)
      if(allocated(dataStruct%var))then
        check=.true.
      else
        allocate(dataStruct%var(nVars),stat=err)
      end if
      return
    ! ****************************************************    
    class is (var_i8)
      if(allocated(dataStruct%var))then
        check=.true.
      else
        allocate(dataStruct%var(nVars), stat=err)
      end if
      return
    ! ****************************************************    
    class is (var_dlength)
      if(allocated(dataStruct%var))then
        check=.true.
      else
        allocate(dataStruct%var(nVars),stat=err)
        call allocateDat_rkind(metaStruct,dataStruct,nSnow,nLake_l,nSoil,nGlce_l,err,cmessage)
      end if
    ! ****************************************************
    class is (var_time_ilength)
      if(allocated(dataStruct%var))then
        check=.true. 
      else 
        allocate(dataStruct%var(nVars),stat=err) 
      end if
      do iVar=1, nVars
        ! Check if this variable is desired within any timeframe
        if(is_var_desired(metaStruct,iVar) .or. allocAllFlag .or. (present(str_name) .and. &
         ((iVar == iLookINDEX%nLayers) .or. (iVar == iLookINDEX%nSnow) .or. (iVar == iLookINDEX%nLake) .or. (iVar == iLookINDEX%nSoil) .or. (iVar == iLookINDEX%nGlce)) ))then
        allocate(dataStruct%var(iVar)%tim(nSteps))
          call allocateDat_int(metaStruct,dataStruct,nSnow,nLake_l,nSoil,nGlce_l,nSteps,iVar,err,cmessage)
        end if
      end do
    ! ****************************************************
    class is (var_time_dlength)
      if(allocated(dataStruct%var))then
        check=.true.
      else 
        allocate(dataStruct%var(nVars),stat=err)
      end if
      do iVar=1, nVars
        ! Check if this variable is desired within any timeframe
        if(is_var_desired(metaStruct,iVar) .or. allocAllFlag)then
          if (allocated(dataStruct%var(iVar)%tim)) then
            print*, "Already Allocated"; return;
          end if
          allocate(dataStruct%var(iVar)%tim(nSteps), stat=err)
          call allocateDat_rkind_nSteps(metaStruct,dataStruct,nSnow,nLake_l,nSoil,nGlce_l,nSteps,iVar,err,cmessage)
        end if
      end do
    ! ****************************************************
    class default; err=20; message=trim(message)//'unable to identify derived data type for the variable dimension'; print*,message;return
  end select
  ! check errors
  if(check) then; err=20; message=trim(message)//'structure was unexpectedly allocated already'; print*,message; return; end if
  if(err/=0)then; err=20; message=trim(message)//'problem allocating'; print*,message; return; end if

  ! check errors
  if(err/=0)then; message=trim(message)//trim(cmessage); print*, message; return; end if
end subroutine

logical function is_var_desired(metaStruct, iVar)
  USE data_types
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  implicit none
  type(var_info),intent(in) :: metaStruct(:)
  integer(i4b),intent(in)   :: iVar
  ! local
  integer(i4b)              :: iFreq
  ! initalize error control
  is_var_desired=.false.
  do iFreq=1,maxvarFreq
    if(metaStruct(iVar)%statIndex(iFreq) /= integerMissing)then
      is_var_desired=.true.
      exit
    end if
  end do

end function is_var_desired

subroutine allocateDat_rkind_nSteps(metadata,varData,nSnow,nLake,nSoil,nGlce, &
  nSteps,iVar,err,message)
  USE data_types
  USE actor_data_types
  USE var_lookup,only:iLookVarType           ! look up structure for variable typed

  USE globalData,only:nTimeDelay            ! number of timesteps in the time delay histogram
  USE globalData,only:nSpecBand                 ! number of spectral bands
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  USE get_ixName_module,only:get_varTypeName       ! to access type strings for error messages

  implicit none
  type(var_info),intent(in)            :: metadata(:)
  ! output variables
  type(var_time_dlength),intent(inout) :: varData     ! model variables for a local HRU
  integer(i4b),intent(in)              :: nSnow,nLake,nSoil,nGlce
  integer(i4b),intent(in)              :: nSteps
  integer(i4b),intent(in)              :: iVar
  integer(i4b),intent(inout)           :: err         ! error code
  character(*),intent(inout)           :: message     ! error message

  ! local variables
  integer(i4b)                         :: iStep
  integer(i4b)                         :: nLayers
  message='allocateDat_rkindAccessActor'

  nLayers = nSnow+nLake+nSoil+nGlce
  do iStep=1, nSteps
    select case(metadata(iVar)%varType)
      case(iLookVarType%scalarv); allocate(varData%var(iVar)%tim(iStep)%dat(1),stat=err)
      case(iLookVarType%wLength); allocate(varData%var(iVar)%tim(iStep)%dat(nSpecBand),stat=err)
      case(iLookVarType%midSnow); allocate(varData%var(iVar)%tim(iStep)%dat(nSnow),stat=err)
      case(iLookVarType%midLake); allocate(varData%var(iVar)%tim(iStep)%dat(nLake),stat=err)
      case(iLookVarType%midSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil),stat=err)
      case(iLookVarType%midGlce); allocate(varData%var(iVar)%tim(iStep)%dat(nGlce),stat=err)
      case(iLookVarType%midToto); allocate(varData%var(iVar)%tim(iStep)%dat(nLayers),stat=err)
      case(iLookVarType%ifcSnow); allocate(varData%var(iVar)%tim(iStep)%dat(nSnow+1),stat=err)
      case(iLookVarType%ifcLake); allocate(varData%var(iVar)%tim(iStep)%dat(nLake+1),stat=err)
      case(iLookVarType%ifcSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil+1),stat=err)
      case(iLookVarType%ifcGlce); allocate(varData%var(iVar)%tim(iStep)%dat(nGlce+1),stat=err)
      case(iLookVarType%ifcToto); allocate(varData%var(iVar)%tim(iStep)%dat(nLayers+1),stat=err)
      case(iLookVarType%parSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil),stat=err)
      case(iLookVarType%routing); allocate(varData%var(iVar)%tim(iStep)%dat(nTimeDelay),stat=err)
      case(iLookVarType%outstat); allocate(varData%var(iVar)%tim(iStep)%dat(maxvarfreq*2),stat=err)
      case(iLookVarType%unknown); allocate(varData%var(iVar)%tim(iStep)%dat(0),stat=err)
      case default
      err=40; message=trim(message)//"1. unknownVariableType[name='"//trim(metadata(iVar)%varname)//"'; type='"//trim(get_varTypeName(metadata(iVar)%varType))//"']"
      return
    end select
  end do ! (iStep)

end subroutine allocateDat_rkind_nSteps

subroutine allocateDat_rkind(metadata,varData,nSnow,nLake,nSoil,nGlce,err,message)
  USE get_ixName_module,only:get_varTypeName       ! to access type strings for error messages
  USE data_types
  USE var_lookup,only:iLookVarType           ! look up structure for variable typed
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  USE globalData,only:nSpecBand                 ! number of spectral bands
  USE globalData,only:nTimeDelay            ! number of timesteps in the time delay histogram
  implicit none
  type(var_info),intent(in)         :: metadata(:)
  ! output variables
  type(var_dlength),intent(inout)   :: varData     ! model variables for a local HRU
  integer(i4b),intent(in)           :: nSnow,nLake,nSoil,nGlce

  integer(i4b),intent(inout)        :: err         ! error code
  character(*),intent(inout)        :: message     ! error message

  ! local variables
  integer(i4b)                      :: nVars
  integer(i4b)                      :: iVar
  integer(i4b)                      :: nLayers
  message='allocateDat_rkindAccessActor'

  nVars = size(metaData)
  nLayers = nSnow+nLake+nSoil+nGlce
  do iVar=1, nVars
    select case(metadata(iVar)%varType)
    case(iLookVarType%scalarv); allocate(varData%var(iVar)%dat(1),stat=err)
    case(iLookVarType%wLength); allocate(varData%var(iVar)%dat(nSpecBand),stat=err)
    case(iLookVarType%midSnow); allocate(varData%var(iVar)%dat(nSnow),stat=err)
    case(iLookVarType%midLake); allocate(varData%var(iVar)%dat(nLake),stat=err)
    case(iLookVarType%midSoil); allocate(varData%var(iVar)%dat(nSoil),stat=err)
    case(iLookVarType%midGlce); allocate(varData%var(iVar)%dat(nGlce),stat=err)
    case(iLookVarType%midToto); allocate(varData%var(iVar)%dat(nLayers),stat=err)
    case(iLookVarType%ifcSnow); allocate(varData%var(iVar)%dat(nSnow+1),stat=err)
    case(iLookVarType%ifcLake); allocate(varData%var(iVar)%dat(nLake+1),stat=err)
    case(iLookVarType%ifcSoil); allocate(varData%var(iVar)%dat(nSoil+1),stat=err)
    case(iLookVarType%ifcGlce); allocate(varData%var(iVar)%dat(nGlce+1),stat=err)
    case(iLookVarType%ifcToto); allocate(varData%var(iVar)%dat(nLayers+1),stat=err)
    case(iLookVarType%parSoil); allocate(varData%var(iVar)%dat(nSoil),stat=err)
    case(iLookVarType%routing); allocate(varData%var(iVar)%dat(nTimeDelay),stat=err)
    case(iLookVarType%outstat); allocate(varData%var(iVar)%dat(maxvarfreq*2),stat=err)
    case(iLookVarType%unknown); allocate(varData%var(iVar)%dat(0),stat=err)
    case default
        err=40; message=trim(message)//"1. unknownVariableType[name='"//trim(metadata(iVar)%varname)//"'; type='"//trim(get_varTypeName(metadata(iVar)%varType))//"']"
        return
    end select
  end do

end subroutine allocateDat_rkind

subroutine allocateDat_int(metadata,varData,nSnow,nLake,nSoil,nGlce, &
                           nSteps,iVar,err,message)
  USE get_ixName_module,only:get_varTypeName       ! to access type strings for error messages
  USE data_types
  USE actor_data_types
  USE var_lookup,only:iLookVarType           ! look up structure for variable typed
  USE var_lookup,only:maxvarFreq             ! allocation dimension (output frequency)
  USE globalData,only:nSpecBand                 ! number of spectral bands
  USE globalData,only:nTimeDelay            ! number of timesteps in the time delay histogram
  implicit none
  type(var_info),intent(in)            :: metadata(:)
  ! output variables
  type(var_time_ilength),intent(inout) :: varData     ! model variables for a local HRU
  integer(i4b),intent(in)              :: nSnow,nLake,nSoil,nGlce
  integer(i4b),intent(in)              :: nSteps
  integer(i4b),intent(in)              :: iVar
  integer(i4b),intent(inout)           :: err         ! error code
  character(*),intent(inout)           :: message     ! error message
  ! local variables
  integer(i4b)                         :: iStep
  integer(i4b)                         :: nLayers
  message='allocateDat_rkindAccessActor'

  nLayers = nSnow+nLake+nSoil+nGlce
  do iStep=1, nSteps
    select case(metadata(iVar)%varType)
      case(iLookVarType%scalarv); allocate(varData%var(iVar)%tim(iStep)%dat(1),stat=err)
      case(iLookVarType%wLength); allocate(varData%var(iVar)%tim(iStep)%dat(nSpecBand),stat=err)
      case(iLookVarType%midSnow); allocate(varData%var(iVar)%tim(iStep)%dat(nSnow),stat=err)
      case(iLookVarType%midLake); allocate(varData%var(iVar)%tim(iStep)%dat(nLake),stat=err)
      case(iLookVarType%midSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil),stat=err)
      case(iLookVarType%midGlce); allocate(varData%var(iVar)%tim(iStep)%dat(nGlce),stat=err)
      case(iLookVarType%midToto); allocate(varData%var(iVar)%tim(iStep)%dat(nLayers),stat=err)
      case(iLookVarType%ifcSnow); allocate(varData%var(iVar)%tim(iStep)%dat(nSnow+1),stat=err)
      case(iLookVarType%ifcLake); allocate(varData%var(iVar)%tim(iStep)%dat(nLake+1),stat=err)
      case(iLookVarType%ifcSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil+1),stat=err)
      case(iLookVarType%ifcGlce); allocate(varData%var(iVar)%tim(iStep)%dat(nGlce+1),stat=err)
      case(iLookVarType%ifcToto); allocate(varData%var(iVar)%tim(iStep)%dat(nLayers+1),stat=err)
      case(iLookVarType%parSoil); allocate(varData%var(iVar)%tim(iStep)%dat(nSoil),stat=err)
      case(iLookVarType%routing); allocate(varData%var(iVar)%tim(iStep)%dat(nTimeDelay),stat=err)
      case(iLookVarType%outstat); allocate(varData%var(iVar)%tim(iStep)%dat(maxvarfreq*2),stat=err)
      case(iLookVarType%unknown); allocate(varData%var(iVar)%tim(iStep)%dat(0),stat=err)
      case default
      err=40; message=trim(message)//"1. unknownVariableType[name='"//trim(metadata(iVar)%varname)//"'; type='"//trim(get_varTypeName(metadata(iVar)%varType))//"']"
      return
    end select
  end do ! loop through time steps
end subroutine allocateDat_int


subroutine f_resetGruStrucNSnow(indx_gru) bind(C, name="f_resetGruStrucNSnow")
  USE globalData, only: gru_struc
  USE summa_init_struc, only: init_struc
  USE var_lookup, only: iLookINDEX
  implicit none
  integer(c_int), intent(in) :: indx_gru
  integer(i4b) :: iHRU, iDOM
  do iHRU = 1, gru_struc(indx_gru)%hruCount
    do iDOM = 1, gru_struc(indx_gru)%hruInfo(iHRU)%domCount
      associate(dom => init_struc%indxStruct%gru(indx_gru)%hru(iHRU)%dom(iDOM))
        gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSnow = dom%var(iLookINDEX%nSnow)%dat(1)
        gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nLake = dom%var(iLookINDEX%nLake)%dat(1)
        gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nSoil = dom%var(iLookINDEX%nSoil)%dat(1)
        gru_struc(indx_gru)%hruInfo(iHRU)%domInfo(iDOM)%nGlce = dom%var(iLookINDEX%nGlce)%dat(1)
      end associate
    end do
  end do
end subroutine f_resetGruStrucNSnow

end module gru_actor
